# Go to Wikidata SPARQL interface: https://query.wikidata.org/


PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>

SELECT ?country ?countryLabel ?population ?date
WHERE {
  
  ?country wdt:P31 wd:Q6256 ;
           p:P1082 ?populationStatement .
  
  ?populationStatement ps:P1082 ?population ;
                       pq:P585 ?date .
  
  FILTER (YEAR(?date) = 2016)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],mul,en". } 
# Helps get the label in your language, if not, then default for all languages, then en language
}
